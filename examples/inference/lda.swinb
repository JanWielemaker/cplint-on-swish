<div class="notebook">
<div class="nb-cell html">
<script type="text/javascript">
$.getScript("https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML",function() {MathJax.Hub.Queue(["Typeset",MathJax.Hub]);});
</script>
<h2>Latent Dirichlet Allocation</h2>

</div>
<div class="nb-cell query">
mc_sample_arg_bar(word(1,1,W),100,W,G).
</div>
<div class="nb-cell query">
mc_sample_arg_bar(word_topic(1,1,WT),100,WT,G).
</div>
<div class="nb-cell query">
mc_mh_sample_arg_bar(word_topic(1,1,WT),word(1,2,1),100,1,WT,G).
</div>
<div class="nb-cell html">
A priori both topics are about equally probable for word 1 of document 1.
</div>
<div class="nb-cell query">
mc_sample_arg_bar(topic(1,1,T),100,T,G).
</div>
<div class="nb-cell html">
After observing that words 1 and 2 of document 1 are equal, one of the
topics gets more probable.
</div>
<div class="nb-cell query">
mc_mh_sample_arg_bar(topic(1,1,T),(word(1,1,1),word(1,2,1)),100,2,T,G).
</div>
<div class="nb-cell program prolog" data-background="true">

:- use_module(library(mcintyre)).

:- if(current_predicate(use_rendering/1)).
:- use_rendering(c3).
:- endif.

:- mc.

:- begin_lpad.

theta(_,Theta):dirichlet(Theta,Alpha):-alpha(Alpha).

topic(DocumentID,_,Topic):discrete(Topic,Dist):-
  theta(DocumentID,Theta),
  topic_list(Topics),
  maplist(pair,Topics,Theta,Dist).

word(DocumentID,WordID,Word):discrete(Word,Dist):-
  topic(DocumentID,WordID,Topic),
  beta(Topic,Beta),
  word_list(Words),
  maplist(pair,Words,Beta,Dist).

word_topic(DocumentID,WordID,(Word,Topic)):-
  topic(DocumentID,WordID,Topic),
  word(DocumentID,WordID,Word).

beta(_,Beta):dirichlet(Beta,Parameters):-
  n_words(N),
  eta(Eta),
  findall(Eta,between(1,N,_),Parameters).

alpha(Alpha):-
  eta(Eta),
  n_topics(N),
  findall(Eta,between(1,N,_),Alpha).

eta(2).

pair(V,P,V:P).

topic_list(L):-
  n_topics(N),
  numlist(1,N,L).

word_list(L):-
  n_words(N),
  numlist(1,N,L).


n_topics(2).

n_words(10).

:-end_lpad.

</div>
<div class="nb-cell html">
Blei, David M., Andrew Y. Ng, and Michael I. Jordan. "Latent dirichlet allocation." Journal of machine Learning research 3.Jan (2003): 993-1022.
</div>
</div>
